#!/usr/bin/perl
# This program is open source, licensed under the simplified BSD license.
# For license terms, see the LICENSE file.

#TODO
# still a lot...

use strict;
use warnings;

use pgShark;
use pgShark::protocol_3;
use Getopt::Long qw(:config bundling no_ignore_case_always);
use Pod::Usage;
use IO::Socket;
use IO::Select;

BEGIN {

    # set the DEBUG constant to 0 or 1
    # When 0, optimize away all debug messages at compile time
    eval q{
        $ENV{'DEBUG'} = 0
            unless defined $ENV{'DEBUG'} and $ENV{'DEBUG'};
        use constant DEBUG => $ENV{'DEBUG'};
    };
}

use constant READMAX      => 2048;

my %sessions;
my $selects = new IO::Select();
my $shark;
my %args = (
    'help'      => 0,
    'interface' => '',
    'host'      => undef,
    'port'      => 5432,
    'read'      => [],
    'rhost'     => undef,
    'rport'     => undef,
    'filter'    => undef,
    'role'      => undef,
    'trydb'     => undef,
);
my %socks_to_sess;

#readFromBackend
# @param $sess_hash Session hash to listen on
# @returns 0 when ready, 1 when nothing happened
sub readFromBackend {
    my $sess_hash = shift;
    my $session   = $sessions{$sess_hash};
    my $sock      = $session->{'sock'};
    my $timeout   = 0;
    my $buff      = '';
    my @ready;

    pgShark::dprint 4 => "readFromBackend: on session $sess_hash.";

    # TODO: better timeout handling
    TIMELOOP: while (1) {
        $timeout++;
        @ready = $selects->can_read();

        foreach my $fh (@ready) {
            my $count = READMAX;
            my $curr_sess = $socks_to_sess{$fh->sockport};

            while ( defined $count and $count == READMAX ) {
                $count = sysread( $fh, $buff, READMAX );
                if ( defined $count ) {
                    pgShark::dprint 4 => "  $count bytes readed.";
                    $sessions{$curr_sess}{'data'} .= $buff;
                }

                # TODO must handle error on $count not defined
            }

            # our socket is ready to be readed
            last TIMELOOP if ( $fh == $sock );
        }
        next TIMELOOP;
    }

    pgShark::dprint 4 => "socket not ready for $timeout loop.";

    return 1 unless $timeout > 0;

    return 0;
}

#fetchUntilReadyForQuery
# @param $sess_hash Session hash to read next message from
# @return last message type on success, or undef
sub fetchUntilReadyForQuery {
    my $sess_hash = shift;
    my $session   = $sessions{$sess_hash};
    my $msg_len;
    my $type;

    do {{
        $type = get_msg_type_backend( $session->{'data'}, {} );

        if ( not defined $type ) {
            return undef if readFromBackend($sess_hash) == 1;
            next ;
        }
        elsif ( $type eq '' ) {
            pgShark::dprint 4 => "fetchUntilReadyForQuery ended with unknown type!";
            return undef;
        }

        $msg_len = get_msg_len( $type, $session->{'data'}, {} );

        if ( $msg_len == 0 ) {
            return undef if readFromBackend($sess_hash) == 1;
            next ;
        }

        $session->{'data'} = substr( $session->{'data'}, $msg_len );
        pgShark::dprint 3 => "Seen a $type as answer to $sess_hash.";
    }} while defined $type and $type ne 'ReadyForQuery';

    return $type;
}

sub pg_connect {
    my $sess_hash = shift;
    my $sock      = $sessions{$sess_hash}{'sock'};
    my $type;

    pgShark::dprint 2 => "session $sess_hash";

    ## authentication
    my $msg
        = "user\0$args{'role'}\0database\0$sessions{$sess_hash}{'database'}\0\0";

    # TODO support protocol 2 ?
    $msg = pack( "NNZ*Z*Z*Z*Z*",
        8 + length($msg),
        196608, "user", $args{'role'}, "database",
        $sessions{$sess_hash}{'database'}, "" );
    $sock->send($msg);

    return 1 if readFromBackend($sess_hash) == 1;

    $type = get_msg_type_backend( $sessions{$sess_hash}{'data'}, {} );

    return 1
        if not defined $type or $type ne 'AuthenticationOk';

    pgShark::dprint 1 => "  $type.";

    fetchUntilReadyForQuery( $sess_hash );

    $sessions{$sess_hash}{'msg_ts'} = 0;

    return 0;
}

sub StartupMessage {
    my $pg_msg    = shift;
    my $sess_hash = $pg_msg->{'sess_hash'};

    pgShark::dprint 3 => "StartupMessage: session $sess_hash.";

    $sessions{$sess_hash}{'database'} = $pg_msg->{'params'}->{'database'};
}

## handle command B(R)
# @param $pg_msg hash with pg message properties
sub openSocket {
    my $pg_msg    = shift;
    my $sess_hash = $pg_msg->{'sess_hash'};

    pgShark::dprint 4 => "openSocket: session $sess_hash";

    # if we don't have information about the session opening, ignore it
    return unless defined $sessions{$sess_hash};

    if ( substr( $args{'rhost'}, 0, 1 ) eq '/' ) {
        my $sock_path = "$args{'rhost'}/.s.PGSQL.$args{'rport'}";

        pgShark::dprint 4 => "  opening unix socket : «$sock_path»";
        $sessions{$sess_hash}{'sock'} = IO::Socket::UNIX->new(
            'Type' => SOCK_STREAM,
            'Peer' => $sock_path
        );
    }
    else {
        pgShark::dprint 4 =>
            "  opening inet socket : «tcp://$args{'rhost'}:$args{'rport'}»";
        $sessions{$sess_hash}{'sock'} = IO::Socket::INET->new(
            'PeerAddr' => $args{'rhost'},
            'PeerPort' => $args{'rport'},
            'Proto'    => 'tcp',
            'Blocking' => 0
        );
    }

    if ( not $sessions{$sess_hash}{'sock'} ) {
        pgShark::dprint 1 => "  could not open socket for session $sess_hash. $!";
        delete $sessions{$sess_hash};
        return;
    }

    pgShark::dprint 4 => "  socket for session $sess_hash.";

    $selects->add( $sessions{$sess_hash}{'sock'} );
    $socks_to_sess{ $sessions{$sess_hash}{'sock'}->sockport } = $sess_hash;

    $sessions{$sess_hash}{'data'} = '';

    if ( pg_connect($sess_hash) ) {
        pgShark::dprint 1 => "  could not open pgsql session for $sess_hash.";
        delete $sessions{$sess_hash};
        return;
    }
}

#Replay
# replay any kind of message
# @param $pg_msg hash with pg message properties
sub Replay {
    my $pg_msg    = shift;
    my $pg_ans    = shift;
    my $sess_hash = $pg_msg->{'sess_hash'};

    unless ( defined $sessions{$sess_hash} ) {
        if ( defined $args{'trydb'} ) {
            pgShark::dprint 2 => "Forcing a sessions for $pg_msg->{'sess_hash'}"
                ." using $args{'trydb'}";
            $sessions{$sess_hash}{'database'} = $args{'trydb'};
            openSocket($pg_msg);
        }
        else {
            return;
        }
    }

    if (    $sessions{$sess_hash}{'msg_ts'}
        and $sessions{$sess_hash}{'msg_ts'} != $pg_msg->{'timestamp'} )
    {
        fetchUntilReadyForQuery( $sess_hash );

        $sessions{$sess_hash}{'msg_ts'} = $pg_msg->{'timestamp'};
    }

    pgShark::dprint 3
        => "Replay: replaying a '$pg_msg->{'type'}' for session $sess_hash.";

    my $sock = $sessions{$sess_hash}{'sock'};

    print $sock $pg_msg->{'data'};
    $sock->flush;

}

#ReplayCopy
# special callback to filter message availables for F OR B
sub ReplayCopy {
    my $pg_msg = shift;

    return if $pg_msg->{'from'} eq 'B';

    Replay($pg_msg);
}

sub Terminate {
    my $pg_msg    = shift;
    my $sess_hash = $pg_msg->{'sess_hash'};

    return unless defined $sessions{$sess_hash};

    my $sock = $sessions{$sess_hash}{'sock'};

    pgShark::dprint 2 => "Terminate: session $sess_hash.";

    fetchUntilReadyForQuery( $sess_hash );

    print $sock $pg_msg->{'data'};

    fetchUntilReadyForQuery( $sess_hash );

    $selects->remove($sock);
    $sock->close();
    delete $sessions{$sess_hash};
}

Getopt::Long::Configure('bundling');
GetOptions(
    \%args, qw{
        help|?!
        interface|i=s
        host|h=s
        port|p=s
        read|r=s
        rhost|rh=s
        rport|rp=s
        filter|F=s
        role|U=s
        trydb|tdb=s
        }
) or pod2usage( -exitval => 2 );

pod2usage( -verbose => 2 ) if $args{'help'};

pod2usage(
    -message => "Arguments --rhost, --rport and --role are mandatory",
    -exitval => 1
    )
    unless defined $args{'rhost'}
        and defined $args{'rport'}
        and defined $args{'role'};

pod2usage(
    -message => "Arguments --interface and --read are incompatible.\n"
        . "Either listen from the network or open a pcap file.",
    -exitval => 1
) if $args{'interface'} ne '' and $args{'read'} ne '';

$args{'rhost'} = '/tmp' if $args{'rhost'} eq '';

$shark = pgShark->new(
    {   'procs' => {
            'AuthenticationOk' => \&openSocket,
            'Bind'             => \&Replay,
            'CancelRequest'    => \&Replay,
            'Close'            => \&Replay,
            'CopyData'         => \&ReplayCopy,
            'CopyDone'         => \&ReplayCopy,
            'CopyFail'         => \&Replay,
            'Describe'         => \&Replay,
            'Execute'          => \&Replay,
            'Flush'            => \&Replay,
            'FunctionCall'     => \&Replay,
            'Parse'            => \&Replay,
            'Query'            => \&Replay,
            'StartupMessage'   => \&StartupMessage,
            'Sync'             => \&Replay,
            'Terminate'        => \&Terminate
        },
        'host'  => $args{'host'},
        'port'  => $args{'port'},
        'debug' => DEBUG
    }
);

if ( defined $args{'filter'} ) {
    # stolen from pgShark core
    my $filter = 'ip and tcp and (%s) and ( '
    . '(((ip[2:2] - ((ip[0]&0xf)<<2)) - ((tcp[12]&0xf0)>>2)) != 0) '
    . 'or (tcp[tcpflags] & (tcp-fin|tcp-rst) != 0) ' . ')';

    $filter = sprintf $filter, $args{'filter'};

    $shark->setFilter( $filter );
}

# open a live capture on given interface
if ( $args{'interface'} ne '' ) {
    my $err = '';

    ( $shark->live( $args{'interface'}, \$err ) == 0 )
        or die("Can not open interface '$args{'interface'}':\n$err");

    $shark->process_all();

    $shark->close();
}

# we have no interface to listen on,
# either open given pcap file or start reading from stdin
else {
    my $err = '';

    # read from stdin if no pcap file given
    $args{'read'}[0] = '-' if scalar @{ $args{'read'} } == 0;

    foreach my $f ( @{ $args{'read'} } ) {

        pgShark::dprint 1 => "Replaying file $f...";

        ( $shark->open( $f, \$err ) == 0 )
            or die("Can not read from file '$f':\n$err");

        $shark->process_all();

        $shark->close();
    }
}

exit 0;

__END__

=head1 NAME

pgs-replay - Replays pgsql messages

Belongs to the pgShark scripts collection.

=head1 SYNOPSIS

=over 2

=item pgs-replay --help

=item pgs-replay --rhost IPADDR --rport PORT --role ROLE [--read file|--interface DEV] [--host IPADDR] [--port PORT]

=back

=head1 DESCRIPTION

B<pgs-replay> send the PostgreSQL messages to a given PostgreSQL cluster. The
network dump could be live or from a pcap file (using tcpdump for instance).

This script only supports protocol v3, making it compatilible with versions 7.4
to 9.2 of PostgreSQL.

This script currently does not support any kind of authentication on the remote PostgreSQL
cluster where messages are send. Make sure it can connect using ident, peer or
trust.

=over 2

=item B<--help>

Show this help message and exit.

=item B<-h>, B<--host> <ip address>

Gives the IP address of the PostgreSQL server. By default, set to 127.0.0.1.

=item B<-p>, B<--port> <port>

Gives the port the PostgreSQL backend is listening on. By default, set to 5432

=item B<-rh>, B<--rhost> <ip address>

Mandatory. Gives the IP address of the remote PostgreSQL server to send the
messages.

=item B<-rp>, B<--rport> <port>

Mandatory. Gives the port of the remote PostgreSQL.

=item B<-U>, B<--role> <role>

Mandatory. Gives the role of the remote PostgreSQL.

=item B<-i>, B<--interface> <interface name>

Capture PostgreSQL traffic directly from the given network interface. Conflict with B<--read>.
By default, B<pgs-replay> will read from stdin if neither B<--read> or B<--interface> are given.

=item B<-r>, B<--read> <path to file>

Read PostgreSQL traffic from given pcap file. Conflict with B<--interface>.
By default, B<pgs-replay> will read from stdin if neither B<--read> or B<--interface> are given.

=back

=head1 AUTHORS

Jehan-Guillaume (ioguix) de Rorthais, jgdr at dalibo dot com.

Dalibo's team.

http://www.dalibo.org

=head1 SEE ALSO

The pgShark wiki on github : https://github.com/dalibo/pgshark/wiki

=head1 LICENSING

This program is open source, licensed under the simplified BSD license. For license terms, see the LICENSE provided
with the sources.

=cut
